stmts = { stmt } ;

stmt = if_stmt
     | while_stmt
     | ID ( ":=" | "=" ) expr ";"
     | ("continue" | "break") ";"
     | [ "return" ] [ expr ] ";"
     ;

if_stmt = "if" expr "{" stmts "}" [ if_stmt_else ] ;
if_stmt_else = "else" (if_stmt | "{" stmts "}") ;

while_stmt = "while" expr "{" stmts "}" ;

expr = if_expr
     | function
     | l_or
     ;

if_expr = "if" expr "{" expr "}" [ if_expr_else ] ;
if_expr_else = "else" (if_expr | "{" expr "}") ;

function = "func" "(" [ ID { "," ID } ] ")" "{" stmts "}" ;

l_or = l_and { "||" l_and } ;

l_and = comp { "&&" comp } ;

comp = term [ ("==" | "!=" | ">" | ">=" | "<" | "<=") term ] ;

term = addend { ("+" | "-") addend } ;

addend = factor { ("*" | "/") factor } ;

factor = { prefix_op } value { suffix_op } ;

prefix_op = "!" | "+" | "-";

suffix_op = "(" [ expr { "," expr } ] ")"
          | "[" expr "]"
          ;

value = "(" expr ")"
      | "[" [ expr { "," expr } ] "]"
      | ID
      | NUM
      | STR
      | "true"
      | "false"
      | "nil"
      ;
